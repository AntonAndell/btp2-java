version = '0.1.0'

dependencies {
    compileOnly("foundation.icon:javaee-api:$javaeeVersion")
    implementation("foundation.icon:javaee-scorex:$scorexVersion")
    implementation project(':score-util')
    implementation project(':lib')

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")

    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation ('org.web3j:core:4.8.7')

    testImplementation("foundation.icon:javaee-unittest:$javaeeUnittestVersion")
    testImplementation project(':test-lib')

    testImplementation 'foundation.icon:icon-sdk:2.0.0'
    testImplementation 'com.squareup.okhttp3:okhttp:3.11.0'
    testImplementation 'foundation.icon:javaee-integration-test:0.9.0'
    testImplementation 'com.google.code.gson:gson:2.10'

}

optimizedJar {
    dependsOn(project(':lib').jar)
    dependsOn(project(':score-util').jar)
    mainClassName = 'foundation.icon.btp.bmv.bsc.BTPMessageVerifier'
    archivesBaseName = 'bmv-bsc'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    enableDebug = debugJar
}

deployJar {
    endpoints {
        gangnam {
            uri = 'https://gicon.net.solidwallet.io/api/v3'
            nid = 7
        }
        local {
            uri = scoreTest.url
            nid = scoreTest.parseNid(scoreTest.nid)
        }
    }
    keystore = scoreTest.default.keyStore
    password = scoreTest.default.resolvedKeyPassword
    parameters {[
        arg('chainId', '0x'),
        arg('epoch', '200'),
        arg('header', '0x'),
        arg('recents', '0x'),
        arg('validators', '0x')
    ]}
}

test {
    useJUnitPlatform {
        if (!integrationTest) {
            excludeTags("integration")
        } else {
            // use the common config files
            systemProperty('env.props', new File('src/test/resources/env.props'))

            def prefix = 'score.path.'
            systemProperty(prefix + 'bmv-' + project.name, optimizedJar.outputJarName)
            dependsOn optimizedJar
            systemProperty prefix + 'bmc-mock.scoreFilePath', tasks.getByPath(":test-lib:optimizedJarMockBMC").outputJarName
        }
    }

    options {
        testLogging.showStandardStreams = true
        systemProperty 'url', scoreTest.url
        systemProperty 'nid', scoreTest.nid
        systemProperty 'keyStore', scoreTest.default.keyStore
        systemProperty 'keyPassword', scoreTest.default.resolvedKeyPassword
//        systemProperty 'address', "cx0000000000000000000000000000000000000000"
//        systemProperty 'isUpdate', "true"
        dependsOn optimizedJar
        systemProperty 'scoreFilePath', project.tasks.optimizedJar.outputJarName
        project.extensions.deployJar.arguments.each {
            arg -> systemProperty 'params.'+arg.name, arg.value
        }

        //for bsh-mock client
        systemProperty 'bmc-mock.url', scoreTest.url
        systemProperty 'bmc-mock.nid', scoreTest.nid
        systemProperty 'bmc-mock.keyStore', scoreTest.default.keyStore
        systemProperty 'bmc-mock.keyPassword', scoreTest.default.resolvedKeyPassword
//        systemProperty 'address', "cx0000000000000000000000000000000000000000"
//        systemProperty 'isUpdate', "true"
        dependsOn ":test-lib:optimizedJarMockBMC"
        systemProperty 'bmc-mock.scoreFilePath', tasks.getByPath(":test-lib:optimizedJarMockBMC").outputJarName
        systemProperty 'bmc-mock.params._net', scoreTest.nid+'.icon'
    }
}
